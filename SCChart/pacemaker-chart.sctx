#resource "timing.h"
#hostcode-c "#include \"timing.h\""

@ClocksUseSD
scchart Pacemaker {
  input bool AS, VS
  output signal AP, VP
  signal AVI, PVARP, VRP, AEI, LRI, URI
  host "int" AVI_Constraint = `AVI_VALUE`
  host "int" PVARP_Constraint = `PVARP_VALUE`
  host "int" VRP_Constraint = `VRP_VALUE`
  host "int" AEI_Constraint = `AEI_VALUE`
  host "int" LRI_Constraint = `LRI_VALUE`
  host "int" URI_Constraint = `URI_VALUE`

  region AVI {
    clock cAVI = 0

    initial state wAVI
    if (AS || AP) && !PVARP go to dAVI

    state dAVI {
      entry do cAVI = 0
      during do AVI
    }
    if (cAVI >= AVI_Constraint && !URI) && !LRI do VP go to wAVI
    if VS && !VRP go to wAVI
  }

  region PVARP {
    clock cPVARP = 0

    initial state wPVARP
    if VS || VP go to dPVARP

    state dPVARP {
      entry do cPVARP = 0
      during do PVARP
    }
    if cPVARP >= PVARP_Constraint go to wPVARP
  }

  region VRP {
    clock cVRP = 0

    initial state wVRP
    if VS || VP go to dVRP

    state dVRP {
      entry do cVRP = 0
      during do VRP
    }
    if cVRP >= VRP_Constraint go to wVRP
  }

  region AEI {
    clock cAEI = 0

    initial state wAEI
    if (VS || VP) && !VRP go to dAEI

    state dAEI {
      entry do cAEI = 0
      during do AEI
    }
    if cAEI >= AEI_Constraint do AP go to wAEI
    if AS && !PVARP go to wAEI
  }

  region LRI {
    clock cLRI = 0

    initial state wLRI
    if (VP || VS) && !VRP go to dLRI

    state dLRI {
      entry do cLRI = 0
      during do LRI
    }
    if (VS || VP) && !VRP go to dLRI
    if cLRI >= LRI_Constraint do VP go to dLRI
  }

  region URI {
    clock cURI = 0

    initial state wURI
    if (VS || VP) && !VRP go to dURI

    state dURI {
      entry do cURI = 0
      during do URI
    }
    if cURI >= URI_Constraint go to wURI
  }
}
 
 
