#resource "timing.h"
#hostcode-c "#include \"timing.h\""

scchart Pacemaker {
  input bool AS, VS
  output signal AP, VP
  bool AVI, PVARP, VRP, AEI, LRI  , URI
  signal eAVI, ePVARP, eVRP, eAEI, eLRI, eURI
  host "int" AVI_Constraint = `AVI_VALUE`
  host "int" PVARP_Constraint = `PVARP_VALUE`
  host "int" VRP_Constraint = `VRP_VALUE`
  host "int" AEI_Constraint = `AEI_VALUE`
  host "int" LRI_Constraint = `LRI_VALUE`
  host "int" URI_Constraint = `URI_VALUE`

  region AVI {
    clock cAVI = 0
    initial state iAVI {
      entry do AVI = false
      exit do AVI = true
    }
    if (AS || eAEI) && !PVARP go to dAVI

    state dAVI {
      entry do cAVI = 0
      exit do eAVI
    }

    if (cAVI >= AVI_Constraint && !URI) || !pre(LRI) do VP go to iAVI
    if VS && !VRP go to iAVI
  }

  region PVARP {
     clock cPVARP = 0
    initial state wPVARP {
      entry do PVARP = false
      exit do PVARP = true
    }

    if pre(eAVI) go to dPVARP

    state dPVARP {
      entry do cPVARP = 0
    }

    if cPVARP >= PVARP_Constraint go to wPVARP

  }

 region VRP { 
   clock cVRP = 0
   initial state wVRP {
     entry do VRP = false
     exit do VRP = true
   }

   if pre(eAVI) go to dVRP

   state dVRP {
     entry do cVRP = 0
   }

   if cVRP >= VRP_Constraint go to wVRP
 }

 region AEI {
   clock cAEI = 0
   initial state wAEI {
     entry do AEI = false
     exit do AEI = true
   }

   if pre(eAVI) go to dAEI

   state dAEI {
     entry do cAEI = 0
   }

   if cAEI >= AEI_Constraint do AP go to wAEI
   if AS do eAEI go to wAEI
 }

 region LRI {
   clock cLRI = 0
   initial state wLRI {
     entry do LRI = false
     exit do LRI = true
   }

   if eAVI go to dLRI

   state dLRI {
     entry do cLRI = 0
   }
   
   if eAVI go to dLRI
   if cLRI >= LRI_Constraint go to wLRI

 }

 region URI {
   clock cURI = 0
   initial state wURI {
     entry do URI = false
     exit do URI = true
   }

   if pre(eAVI) go to dURI

   state dURI {
     entry do cURI = 0
   }
   
   if cURI >= URI_Constraint go to wURI
 }
 
}
 
 
